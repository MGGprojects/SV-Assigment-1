-- Members:
-- Mauro Gómez Guillén
-- Solomon Fanta
-- Test file for two trains

MODULE Points(goal_straight,goal_curved)
  VAR
    status : {straight,moving,curved};
  ASSIGN
    init(status) := moving;
    next(status) := case
      goal_straight & status = curved : moving;
      goal_curved & status = straight : moving;
      status = moving & goal_straight : {moving, straight};
      status = moving & goal_curved   : {moving, curved};
      TRUE : status;
    esac;
	
	FAIRNESS !(status = moving)

MODULE Train (
        train_id,
        may_approach_A, may_approach_B,
        S2A_red,S3A_red,S4A_red,
        S2B_red,S3B_red,S4B_red,
        S2A_green,S3A_green,S4A_green,
        S2B_green,S3B_green,S4B_green,
        S1A_red,S1A_green,S1B_red,S1B_green,
        P1A_locked_straight,P1A_locked_curved,
        P1B_locked_straight,P1B_locked_curved,
        P2A_locked_straight,P2A_locked_curved,
        P2B_locked_straight,P2B_locked_curved
  )
  VAR
    location : {T1A,T2A,T3A,T4A,T1B,T2B,T3B,T4B,T0A,T0B,T00};
    in_bound : boolean;
  ASSIGN
    init(in_bound) := TRUE;
    next(in_bound) := case
      location=T00 : TRUE;
      location=T3A : FALSE;
      location=T3B : FALSE;
      TRUE : in_bound;
    esac;
    init(location) := T00;
    next(location) := case
      location=T00 &  may_approach_A &  may_approach_B : { T00 , T0A , T0B };
      location=T00 &  may_approach_A & !may_approach_B : { T00 , T0A };
      location=T00 & !may_approach_A &  may_approach_B : { T00 , T0B };
      location=T00 & !may_approach_A & !may_approach_B : T00;
      
      location=T0A & in_bound  : T1A;
      location=T0A & !in_bound : T00;
      location=T0B & in_bound  : T1B;
      location=T0B & !in_bound : T00;
      
      location=T1A & in_bound  & S2A_green : T2A;
      location=T1A & !in_bound & S1A_green : T0A;
      location=T1B & in_bound  & S2B_green : T2B;
      location=T1B & !in_bound & S1B_green : T0B;
      
      location=T2A & in_bound & P1A_locked_straight : T3A ;
      location=T2A & in_bound & P1A_locked_curved : T4B ;
      location=T2A & !in_bound : T1A;
      location=T2B & in_bound & P1B_locked_straight : T3B ;
      location=T2B & in_bound & P1B_locked_curved : T4A ;
      location=T2B & !in_bound : T1B;

      location=T4A & in_bound : T3A ;
      location=T4A & !in_bound & P2A_locked_curved : T2B ;
      location=T4B & in_bound : T3B ;
      location=T4B & !in_bound & P2B_locked_curved : T2A ;
      
      location=T3A & !in_bound & S4A_green : T4A;
      location=T3B & !in_bound & S4B_green : T4B;

      TRUE:location;
    esac;


MODULE Environment(
        S2A_red,S3A_red,S4A_red,
        S2B_red,S3B_red,S4B_red,
        S2A_green,S3A_green,S4A_green,
        S2B_green,S3B_green,S4B_green,
        P1A_goal_straight,P1A_goal_curved,
        P1B_goal_straight,P1B_goal_curved,
        P2A_goal_straight,P2A_goal_curved,
        P2B_goal_straight,P2B_goal_curved    
    )
    
  DEFINE
    -- Combined track occupation (any train)
    T1A_occupied := train1.location = T1A | train2.location = T1A;
    T2A_occupied := train1.location = T2A | train2.location = T2A;
    T3A_occupied := train1.location = T3A | train2.location = T3A;
    T4A_occupied := train1.location = T4A | train2.location = T4A;
    T1B_occupied := train1.location = T1B | train2.location = T1B;
    T2B_occupied := train1.location = T2B | train2.location = T2B;
    T3B_occupied := train1.location = T3B | train2.location = T3B;
    T4B_occupied := train1.location = T4B | train2.location = T4B;
    
    -- Signal occupancy (entry signals are occupied when any train is on entry track)
    S1A_red   := train1.location = T0A | train2.location = T0A; 
    S1B_red   := train1.location = T0B | train2.location = T0B;
    S1A_green := !S1A_red;
    S1B_green := !S1B_red;
    
    -- Point status
    P1A_locked_straight := P1A.status=straight;
    P1B_locked_straight := P1B.status=straight;
    P2A_locked_straight := P2A.status=straight;
    P2B_locked_straight := P2B.status=straight;
    P1A_locked_curved := P1A.status=curved;
    P2A_locked_curved := P2A.status=curved;
    P1B_locked_curved := P1B.status=curved;
    P2B_locked_curved := P2B.status=curved;
    
    -- Approach restrictions to avoid collisions on entry
    may_approach_A := !(train1.location = T3A | train2.location = T3A | 
                       (train1.location = T4A & !train1.in_bound) | 
                       (train2.location = T4A & !train2.in_bound));
    
    may_approach_B := !(train1.location = T3B | train2.location = T3B |
                       (train1.location = T4B & !train1.in_bound) |
                       (train2.location = T4B & !train2.in_bound));
    
  VAR
    P1A : Points(P1A_goal_straight,P1A_goal_curved);
    P1B : Points(P1B_goal_straight,P1B_goal_curved);
    P2A : Points(P2A_goal_straight,P2A_goal_curved);
    P2B : Points(P2B_goal_straight,P2B_goal_curved);
    train1 : Train(
        1, -- train_id
        may_approach_A, may_approach_B,
        S2A_red,S3A_red,S4A_red,
        S2B_red,S3B_red,S4B_red,
        S2A_green,S3A_green,S4A_green,
        S2B_green,S3B_green,S4B_green,
        S1A_red,S1A_green,S1B_red,S1B_green,
        P1A_locked_straight,P1A_locked_curved,
        P1B_locked_straight,P1B_locked_curved,
        P2A_locked_straight,P2A_locked_curved,
        P2B_locked_straight,P2B_locked_curved
    );
    train2 : Train(
        2, -- train_id
        may_approach_A, may_approach_B,
        S2A_red,S3A_red,S4A_red,
        S2B_red,S3B_red,S4B_red,
        S2A_green,S3A_green,S4A_green,
        S2B_green,S3B_green,S4B_green,
        S1A_red,S1A_green,S1B_red,S1B_green,
        P1A_locked_straight,P1A_locked_curved,
        P1B_locked_straight,P1B_locked_curved,
        P2A_locked_straight,P2A_locked_curved,
        P2B_locked_straight,P2B_locked_curved
    );

  -- Collision prevention constraint: no two trains on same track
  TRANS !(next(train1.location = T1A) & next(train2.location = T1A));
  TRANS !(next(train1.location = T2A) & next(train2.location = T2A));
  TRANS !(next(train1.location = T3A) & next(train2.location = T3A));
  TRANS !(next(train1.location = T4A) & next(train2.location = T4A));
  TRANS !(next(train1.location = T1B) & next(train2.location = T1B));
  TRANS !(next(train1.location = T2B) & next(train2.location = T2B));
  TRANS !(next(train1.location = T3B) & next(train2.location = T3B));
  TRANS !(next(train1.location = T4B) & next(train2.location = T4B));

MODULE Interlocking(
        T1A_occupied,T2A_occupied,T3A_occupied,T4A_occupied,
        T1B_occupied,T2B_occupied,T3B_occupied,T4B_occupied,
        S1A_red,S1A_green,S1B_red,S1B_green,
        P1A_locked_straight,P1A_locked_curved,
        P1B_locked_straight,P1B_locked_curved,
        P2A_locked_straight,P2A_locked_curved,
        P2B_locked_straight,P2B_locked_curved
    )
  DEFINE
    S2A_green := !S2A_red;
    S3A_green := !S3A_red;
    S4A_green := !S4A_red;
    S2B_green := !S2B_red;
    S3B_green := !S3B_red;
    S4B_green := !S4B_red;
  VAR
    S2A_red   : boolean;
    S3A_red   : boolean;
    S4A_red   : boolean;
    S2B_red   : boolean;
    S3B_red   : boolean;
    S4B_red   : boolean;
    P1A_goal_straight : boolean;
    P2A_goal_straight : boolean;
    P1B_goal_straight : boolean;
    P2B_goal_straight : boolean;
    P1A_goal_curved : boolean;
    P2A_goal_curved : boolean;
    P1B_goal_curved : boolean;
    P2B_goal_curved : boolean;
  ASSIGN
    init(S2A_red) := TRUE;
    init(S3A_red) := TRUE;
    init(S4A_red) := TRUE;
    init(S2B_red) := TRUE;
    init(S3B_red) := TRUE;
    init(S4B_red) := TRUE;
    init(P1A_goal_straight) := FALSE;
    init(P2A_goal_straight) := FALSE;
    init(P1B_goal_straight) := FALSE;
    init(P2B_goal_straight) := FALSE;
    init(P1A_goal_curved) := FALSE;
    init(P2A_goal_curved) := FALSE;
    init(P1B_goal_curved) := FALSE;
    init(P2B_goal_curved) := FALSE;
    
    -- Signal S2A: Allow green when path is clear
    next(S2A_red) := case
        P1A_locked_straight & P2B_locked_straight & !T2A_occupied & !T3A_occupied & !T1B_occupied : FALSE;
        P1A_locked_curved & P2B_locked_curved & !T2A_occupied & !T4B_occupied & !T3B_occupied & !T2B_occupied : FALSE;
        TRUE : TRUE;
    esac;
    
    next(S3A_red) := case
        P1A_locked_straight & P2B_locked_straight & !T2A_occupied & !T1A_occupied & !T3B_occupied : FALSE;
        TRUE : TRUE;
    esac;
    
    next(S4A_red) := case
        P2A_locked_curved & P1B_locked_curved & !T4A_occupied & !T2B_occupied & !T1B_occupied & !T3A_occupied : FALSE;
        TRUE : TRUE;
    esac;
    
    next(S2B_red) := case
        P1B_locked_straight & P2A_locked_straight & !T2B_occupied & !T3B_occupied & !T1A_occupied : FALSE;
        P1B_locked_curved & P2A_locked_curved & !T2B_occupied & !T4A_occupied & !T3A_occupied & !T2A_occupied : FALSE;
        TRUE : TRUE;
    esac;
    
    next(S3B_red) := case
        P1B_locked_straight & P2A_locked_straight & !T2B_occupied & !T1B_occupied & !T3A_occupied : FALSE;
        TRUE : TRUE;
    esac;
    
    next(S4B_red) := case
        P2B_locked_curved & P1A_locked_curved & !T4B_occupied & !T2A_occupied & !T1A_occupied & !T3B_occupied : FALSE;
        TRUE : TRUE;
    esac;

    -- Point control logic
    next(P1A_goal_straight) := case
      P1A_locked_straight : FALSE;
      T1A_occupied & !T3B_occupied & !T4A_occupied : TRUE;
      T3B_occupied : TRUE;
      TRUE : P1A_goal_straight;
    esac;
    
    next(P1A_goal_curved) := case
      P1A_locked_curved : FALSE;
      T1A_occupied & (T3A_occupied | T1B_occupied) : TRUE;
      T3A_occupied : TRUE;
      TRUE : P1A_goal_curved;
    esac;
    
    next(P2A_goal_straight) := case
      P2A_locked_straight : FALSE;
      T1B_occupied & !T3A_occupied & !T4B_occupied : TRUE;
      T3A_occupied : TRUE;
      TRUE : P2A_goal_straight;
    esac;
    
    next(P2A_goal_curved) := case
      P2A_locked_curved : FALSE;
      T1B_occupied & (T3B_occupied | T1A_occupied) : TRUE;
      T3B_occupied : TRUE;
      TRUE : P2A_goal_curved;
    esac;
    
    next(P1B_goal_straight) := case
      P1B_locked_straight : FALSE;
      T1B_occupied & !T3B_occupied & !T4A_occupied : TRUE;
      T3B_occupied : TRUE;
      TRUE : P1B_goal_straight;
    esac;
    
    next(P1B_goal_curved) := case
      P1B_locked_curved : FALSE;
      T1B_occupied & (T3A_occupied | T1A_occupied) : TRUE;
      T3A_occupied : TRUE;
      TRUE : P1B_goal_curved;
    esac;
    
    next(P2B_goal_straight) := case
      P2B_locked_straight : FALSE;
      T1A_occupied & !T3A_occupied & !T4B_occupied : TRUE;
      T3A_occupied : TRUE;
      TRUE : P2B_goal_straight;
    esac;
    
    next(P2B_goal_curved) := case
      P2B_locked_curved : FALSE;
      T1A_occupied & (T3B_occupied | T1B_occupied) : TRUE;
      T3B_occupied : TRUE;
      TRUE : P2B_goal_curved;
    esac;

MODULE main
  DEFINE
    T1A_occupied := environment.T1A_occupied;
    T2A_occupied := environment.T2A_occupied;
    T3A_occupied := environment.T3A_occupied;
    T4A_occupied := environment.T4A_occupied;
    T1B_occupied := environment.T1B_occupied;
    T2B_occupied := environment.T2B_occupied;
    T3B_occupied := environment.T3B_occupied;
    T4B_occupied := environment.T4B_occupied;
    S1A_red   := environment.S1A_red;
    S1B_red   := environment.S1B_red;
    S1A_green := environment.S1A_green;
    S1B_green := environment.S1B_green;
    P1A_locked_straight := environment.P1A_locked_straight;
    P2A_locked_straight := environment.P2A_locked_straight;
    P1B_locked_straight := environment.P1B_locked_straight;
    P2B_locked_straight := environment.P2B_locked_straight;
    P1A_locked_curved := environment.P1A_locked_curved;
    P2A_locked_curved := environment.P2A_locked_curved;
    P1B_locked_curved := environment.P1B_locked_curved;
    P2B_locked_curved := environment.P2B_locked_curved;

    S2A_red   := interlocking.S2A_red;
    S3A_red   := interlocking.S3A_red;
    S4A_red   := interlocking.S4A_red;
    S2B_red   := interlocking.S2B_red;
    S3B_red   := interlocking.S3B_red;
    S4B_red   := interlocking.S4B_red;
    S2A_green := interlocking.S2A_green;
    S3A_green := interlocking.S3A_green;
    S4A_green := interlocking.S4A_green;
    S2B_green := interlocking.S2B_green;
    S3B_green := interlocking.S3B_green;
    S4B_green := interlocking.S4B_green;
    P1A_goal_straight := interlocking.P1A_goal_straight;
    P2A_goal_straight := interlocking.P2A_goal_straight;
    P1B_goal_straight := interlocking.P1B_goal_straight;
    P2B_goal_straight := interlocking.P2B_goal_straight;
    P1A_goal_curved := interlocking.P1A_goal_curved;
    P2A_goal_curved := interlocking.P2A_goal_curved;
    P1B_goal_curved := interlocking.P1B_goal_curved;
    P2B_goal_curved := interlocking.P2B_goal_curved;
  VAR
    environment  : Environment(
        S2A_red,S3A_red,S4A_red,
        S2B_red,S3B_red,S4B_red,
        S2A_green,S3A_green,S4A_green,
        S2B_green,S3B_green,S4B_green,
        P1A_goal_straight,P1A_goal_curved,
        P1B_goal_straight,P1B_goal_curved,
        P2A_goal_straight,P2A_goal_curved,
        P2B_goal_straight,P2B_goal_curved     
    );
    interlocking : Interlocking(
        next(T1A_occupied),next(T2A_occupied),next(T3A_occupied),next(T4A_occupied),
        next(T1B_occupied),next(T2B_occupied),next(T3B_occupied),next(T4B_occupied),
        next(S1A_red),next(S1A_green),next(S1B_red),next(S1B_green),
        next(P1A_locked_straight),next(P1A_locked_curved),
        next(P1B_locked_straight),next(P1B_locked_curved),
        next(P2A_locked_straight),next(P2A_locked_curved),
        next(P2B_locked_straight),next(P2B_locked_curved)
    );

-- Key properties for two trains
CTLSPEC EF T3A_occupied
CTLSPEC EF T3B_occupied

-- No collisions - no two trains on same track
LTLSPEC G !(environment.train1.location = T1A & environment.train2.location = T1A)
LTLSPEC G !(environment.train1.location = T2A & environment.train2.location = T2A)
LTLSPEC G !(environment.train1.location = T3A & environment.train2.location = T3A)
LTLSPEC G !(environment.train1.location = T4A & environment.train2.location = T4A)
LTLSPEC G !(environment.train1.location = T1B & environment.train2.location = T1B)
LTLSPEC G !(environment.train1.location = T2B & environment.train2.location = T2B)
LTLSPEC G !(environment.train1.location = T3B & environment.train2.location = T3B)
LTLSPEC G !(environment.train1.location = T4B & environment.train2.location = T4B)

-- Trains can meet in the middle (crossing)
SPEC EF (T3A_occupied & T3B_occupied)
